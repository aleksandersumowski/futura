= futura - The building blocks for asynchronous programming in Clojure.
Andrey Antukh, <niwi@niwi.be>
:toc: left
:toclevels: 2
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:


== Introduction

The basic building blocks for concurrent, asynchronous and non blocking programming in clojure.


== Rationale

This project exists as consecuence of the need of good abstraction for represent eventually available
asynchronous values for the link:https://github.com/funcool/catacumba[catacumba project].

It there already existing libraries in clojure ecosystem such as _manifold_ that are well
established and intends to be a "lingua franca" for asynchronous computation results. But I think
that here there space for an alternative with different vision. For see more detailed information
about the differences with _manifold_, see <<difference-with-manifold,this>> faq entry.

The _futura_ library at this moment provides three different building blocks:

* *Streams* +
  I did not want invent yet an other streams abstraction and luckily I found
  the link:http://www.reactive-streams.org/[reactive streams] initiative that has already defined
  a strong and well defined contract on how the abstraction should behave and with backpressure
  awareness in its core. +
  This library uses that interfaces and provides a powerfull clojure features (like transducers) to
  the good and interoperable stream abstraction.
* *Promises* +
  With promises happens something similar, but in this case JDK8 comes with good implementation with
  wrong name: `CompletableFuture`. This library provides a promise abstraction primitive build on top
  of jdk8 completable futures implementation and implements the monadic protocols of cats library that
  allows it to play in the monadic composition using generic syntactic abstractions like haskell
  like `do syntax`.
* *Atomics* +
  Additionally to the mentioned abstractions, this library provides an lighweight clojure friendly
  syntax for working with JDK Atomic types and references that are often used as concurrency primitives
  in the streams implementation.


== Project Maturity

Since _futura_ is a young project there may be some API breakage.


== Install

The simplest way to use _futura_ library in a ClojureScript project is by including
it as a dependency:

[source, clojure]
----
[funcool/futura "0.2.0"]
----


== Promises

Is the abstraction that represents the result of an asynchronous operation that will be eventually
available.

Clojure comes with a builtin promise abstraction but it is designed only blocking operations, and in
our circumstances it is very limited approach. The _futura_'s promise abstraction uses the jdk8
completable future as underlying implementation and all promises can be easily coerced to
jdk future or completable future.

The promise consists in a container that eventually will contain a value with builtin support for
error handling. So the promise has three different states:

- `pending`: means that the promise does not contains any value.
- `resolved`: means that the promise contains a value.
- `rejected`: means thet the promise contains an error.


=== Creating a promise.

It there several different ways to create a promise in _futura_ library. You can create it inmediatelly
resolved with initial value, inmediatelly rejected with some exception.

Let start with a basic example using the commonly known promise delivering in clojure:

[source, clojure]
----
(require '[futura.promise :as p])

(def pr (p/promise))

(future
  (Thread/sleep 200)
  (p/deliver pr 20))]

(p/then pr (fn [v]
             (println v)))

;; After 200ms it will print `20`
----

An other way to create a promise is using a factory function that can resolve or reject
promise in asynchrnous way. If you are familiar with javascript promises, you will found
that very similar:

[source, clojure]
----
(def pr (p/promise
          (fn [deliver]
            (deliver 1))))
----


=== Blocking operations

The _futura_'s promises can be used as drop in replacement for clojure promises, because them offers
also blocking operations:

[source, clojure]
----
@pr
;; => 1
----

If you try to deref a promise that is rejected, the exception will be rereaised in the
calling thread.


=== State checking

_futura_ provides useful predicates that will allow check the state of a promise in any time.

Let see some examples:

[source, clojure]
----
(def pr (p/promise 2))

(p/promise? pr)
;; => true

(p/pending? pr)
;; => false

(p/resolved? pr)
;; => true

(p/rejected? pr)
;; => false

(p/done? pr)
;; => true
----

The `done?` predicate checks if a promise is fullfiled, independently if is resolved or rejected.


=== Promise chaining

_Futura_ also offers powerful chaining methods for allow easy compose async computations. In
previous examples we have seen `then` function, let see an other more complex example using it:

[source, clojure]
----
(def pr (-> (p/promise 2)
            (p/then inc)
            (p/then inc)))

(p/then pr (fn [v]
             (println v)))

;; It will print 4.
----

=== Error handling

It also exposes a chain method for error handling:

[source, clojure]
----
(def pr (-> (p/promise 2)
            (p/then (fn [v] (throw (ex-info "foobar" {}))))))

(p/catch pr (fn [error]
              (println "Error:" (.getMessage error))))
;; Will print something like "Error: foobar"
----

The `catch` chain function also return a promise. Promise that will be resolved or rejected
depending on that will happen inside the catch handler.


=== Working with collections

In some circumstances you will want wait a completion of few promises at same time, and _futura_
also provides helpers for that:

[source, clojure]
----
@(p/all [(p/promise 1) (p/promise 2)])
;; => [1 2]

@(p/any [(p/promise 1) (p/promise (ex-info "error" {}))])
;; => 1
----


== Streams

The streams in _futura_ library are governed by the
link:http://www.reactive-streams.org/[reactive streams] iniciative and its default interfaces
for the jvm languages. Additionally, it comes with support for the the powerful clojure's features as
transducers and sequence abstractions and with great interoprability with existing clojure libraries.

In summary, is a reactive-streams implementation on top of clojure abstractions and works as
intermediary between clojure and the java world.

The reactive streams has four participants, but in the clojure part we only need one: the Publisher.


=== First contact

The _futura_'s streams api is really very simple. It consists in ver few functions.

Let's go to create our first publisher:

[source, clojure]
----
(require '[futura.stream :as stream])

(def s (stream/publisher))
----

The `publisher` function without additional parameters creates a empty, and unbuffered publisher. Now
you can put the items to the using the `put!` function:

[source, clojure]
----
(def prm (stream/put! s 1))
----

The return value of the `put!` function is a promise that will be resolved to true when the value is
accepted by the publisher. And in case of the unbuffered publisher it will happens when one subscription
will request a value.

And finally, for obtain values from the publisher, you should create a subscription:

[source, clojure]
----
(with-open [sub (s/subscribe s)]
  (s/take! s))
----

The return value of `take!` function is also a promise and will be resolved with a value when the first
one will be available.

Take care that the `subscribe` function it is not analogous to the `.subscribe` method of the publisher.
Instead of creating opaque object that acts as relation betwen the publisher and the subscriber, the
`subscribe` function creates a open object that does not has direct relation with any subscriber.

Obviously, behind the schenes the `subscribe` function uses the publishers `.subscribe` method. So the
publisher implementation is completelly interopreable with other third party libraries and the java world.

This subscription strategy has some advantages over the purposed by the reactive-streams, because you can
treat it like a subscription stream. It implements convenient interfaces for the ability to treat the
subscription like a clojure sequence, java iterable or even core.async channel.

You should known that everything in this implementation is lazy. Creating subscription does not
automatically request all items from publisher. A subscription will only request a item to the publisher
when you are request it.

Behind the scenes, publisher and subscription are implemented using _core.async_, so the nil values
are does not allowed and represent that the publisher or the subscription is closed. The subscription
as you can observe implements the `.close()` method that allows use it with `with-open` macro, but take
care, the publisher can end early and subscrion will be closed also before with-open calls the
`.close()` method.


=== Source the publisher

As we said previously, the real purpose of this abstraction is using it as connection with java world,
surelly if you are clojure only user you may want use _core.async_ or _manifold_ directly without
additional layer.

In fact, _futura_ is actually used by _catacumba_ for comunicating asyncronous streams with
ratpack/netty. This abstraction is choiced because it has support for backpressure that is very
important in asynchronous network applications.

But for use it as a intermediary layer with third party libraries you should will be able create
publisher from the existing abstractions in clojure. Now, let se how you can do it:

.Example creating a publisher instance from any object that implements `Iterable`:
[source, clojure]
----
(def pub (p/publisher (take 5 (iterate inc 1))))

(into [] pub)
;; => [1 2 3 4 5]
----

As you can observe the previous example, you can see that the publisher can be easily converted
to sequence. That happens because publisher implements the clojure's `Seqable` interface that behind
the scenes uses a subscription and blocking access to all items until the subscription is closed.

So, you can create a publisher from this kind of abstractions: *promise*, jdk8 *completable future*,
manifold *deferred*, manifold *stream*, *sequences* and *iterables*.


=== Composable trasnformations

Additionally to the previously mentioned abstractions, you can create a publisher from another
publisher using the `transform` function. This kind of composition allows you attach a transducer
for apply some kind of transformations to the stream values.

Take care de some implementation detail: the transducer is applied for subscriptions, not for the
publisher. So, if you use some kind of `(take N)` transducer with possible infinite publisher, it will
cause that each subscription will be closed after `N` items but the publisher will remain active
accepting new subscriptions.

[source, clojure]
----
(def pub (->> (p/publisher (take 5 (iterate inc 1)))
              (p/transform (map inc))))

(into [] pub)
;; => [2 3 4 5 6]
----


=== Subscription as channel

One interesting thing is that open subscriptions can be used as channels so them are fully compatible
for usage in _core.async_ go block like any other channel:

[source, clojure]
----
(require '[clojure.core.async :refer [<! go-loop]])

(with-open [sub (s/subscribe s)]
  (go-loop []
    (when-let [value (<! sub)]
      (do-something value)
      (recur))))
----


== Atomics

This is a simple clojure friendly syntax and lightweight abstraction built on top of clojure's protocols
for treat with JDK atomic types and reference types.

This is a low level, side effecting primitive and should be used only when you are really known that
are you doing. If you do not know if you should use it or not, prefer using standard clojure primitives
such as atom, ref, and agents.


=== Atomic Reference

The atomic reference privides a lock-free, thread safe object reference container. The real purpose
of this type is store a reference to an arbitrary object and will be able mutate it in a thread
safe, lock-fre way.

==== Creating an atomic reference

The atomic reference can be created with `ref` function from the `futura.atomic` namespace:

[source, clojure]
----
(require '[futura.atomic :as atomic])

(atomic/ref :foo)
;; => #object[futura.atomic.AtomicRef 0x5e42bd13 {:status :ready, :val :foo}]
----

//^ And it accepts different kind of typical operatons of atomic abstractions:

==== Get and set values

The atomic reference provides the standard way to set or get values using `get` and `set!` functions:

[source, clojure]
----
(atomic/set! myref :baz)

(atomic/get myref)
;; :baz
----

Additionally it also implements the clojure `IDeref` interface for make it easy use with `@` reader
macro or `deref` function:

[source, clojure]
----
(def myref (atomic/ref :foo))

(deref myref)
;; => :foo

@myref
;; => :foo
----


==== Special operations

The atomic types usually offers some special operations, and this one is not an exception. The atomic
refernce allow CAS operations (compare and set):

[source, clojure]
----
(atomic/compare-and-set! myref :baz :foobar)
;; => false

(atomic/compare-and-set! myref :foo :bar)
;; => true
----

And the "get and set" operation:

[source, clojure]
----
(atomic/get-and-set! myref :foo)
;; => :bar

@myref
;; => :foo
----

Additionaly it provides a way to set a value in some kind of "asynchronously" way. But take care, this
method to set the value does not guarrantee that the change is visible instantly to all threads:

[source, clojure]
----
(atomic/eventually-set! myref :foobar)
----


=== Atomic Boolean

This is a specialized version of Atomic Reference that is higtly optimized for boolean values. It
has the same operations and implements the same abstractions that previously explained atomic ref.

You can create an atomic boolean using `boolean` function from `futura.atomic` namespace:

[source, clojure]
----
(atomic/boolean false)
;; => #object[futura.atomic.AtomicBoolean 0x393bbfce {:status :ready, :val false}]
----


=== Atomic Long

This is a specialized version of Atomic Reference that is highly optimized for numeric operations
with longs. It has the same operations and implements the same abstractions that previously explained
`ref` and `boolean` atomic types.

But additionally implements a complementary abstraction that brings some powerfull operations
that only fits for numeric types, such as atomic counters and similars.


You can create an atomic long using `long` function from `futura.atomic` namespace:

[source, clojure]
----
(atomic/long 0)
;; => #object[futura.atomic.AtomicLong 0x393bbfce {:status :ready, :val 0}]
----


Here some examples using the functions defined for numeric atomic types, such as
"get and increment/decrement" operations:

[source, clojure]
----
(def mylong (atomic/long 0))

(atomic/get-and-inc! mylong)
;; => 0

(atomic/get-and-dec! mylong)
;; => 1

@mylong
;; => 0
----

And optionally you can increment it with user specified delta:

[source, clojure]
----
(atomic/get-and-add! mylong 10)
;; => 0

@mylong
;; => 10
----


== FAQ

[[difference-with-manifold]]
=== What is the difference with manifold

Both libraries offers similar abstractions, _futura_ offers promises and streams and _manifold_ offers
deferreds and streams. The main difference of this libraries is clearly philosoficaly:

- _manifold_ build own abstraction for work with streams, _futura_ uses an existing and interoprable
  abstraction.
- _manifold_ implements its own defferred, _futura_ uses the already one defined in jdk8 (completable future).
- _manifold_ raises own syntax abstraction (`let-flow` and similars), _futura_ implements an existing
  monad abstraction from link:https://github.com/funcool/cats[cats library] that alread offers generic
  let like syntax that serves for compose asynchronous computations that looks sync.


The _futura_ library obviosly is less mature that _manifold_ because of obvios reasons that _manifold_
exists some time ago.


== Contribute

**futura** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just open a issue or pull request.


== Get the Code

_futura_ is open source and can be found on link:https://github.com/funcool/futura[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/futura
----





== Run tests

For run tests just execute this:

[source, text]
----
lein test
----


== License

_futura_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015 Andrey Antukh <niwi@niwi.be>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
